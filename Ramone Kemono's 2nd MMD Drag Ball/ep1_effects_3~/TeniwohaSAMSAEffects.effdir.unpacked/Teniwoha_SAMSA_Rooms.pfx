include "Teniwoha_SAMSA_Time_Stamps.pfx"
create Teniwoha_SAMSA_Time_Stamps(0)

include "Teniwoha_SAMSA_Assets.pfx"
include "Teniwoha_SAMSA_Assets_Butterflies.pfx"
include "Teniwoha_SAMSA_Assets_Magic_Circle.pfx"
include "Teniwoha_SAMSA_Assets_Walls_Floors.pfx"

#clean room
setf cabwidth 0.82*$furnituresize/2
seti cabinrowsize 2#3
setf cabinrowgap 5
seti cabinrownum 3
setf cabindisplace 15
effect cabinetgroup -rigid
	effect Cabinet2 -offset (-$cabwidth,0,0)
	effect Cabinet1 -offset ($cabwidth,0,0)
end

define cabinetrowlisting(i,N)
	effect cabinetgroup -offset((0.5+&{i})*$cabwidth*4,0,0)
	effect cabinetgroup -offset(-(0.5+&{i})*$cabwidth*4,0,0)
enddef
effect cabinetrow -rigid
	arrayCreate cabinetrowlisting $cabinrowsize
end

effect cabinetrowreoriented -rigid
	effect cabinetrow -rotateZ -90 -offset (0.5*$furnituresize/2,0.25*$furnituresize/2,0)
	effect cabinetrow -rotateZ 90 -offset (-0.5*$furnituresize/2,-0.25*$furnituresize/2,0)
end

define cabinetassemblelisting(i,N)
	effect cabinetrowreoriented -offset ((0.5+&{i})*$cabinrowgap,$cabindisplace,0)
	effect cabinetrowreoriented -offset (-(0.5+&{i})*$cabinrowgap,$cabindisplace,0)
enddef

effect cabinexhibit -rigid
	arrayCreate cabinetassemblelisting $cabinrownum
end

effect furnishing -rigid
	effect cabinexhibit
end
	#doors
effect RamoneWoodDoormirrored
	effect RamoneWoodDoor -rotateZ 180
end

effect woodendoors	
	effect RamoneWoodDoor -offset (0,-4*$floorlength*$floortilesize*$tile1scale,0)
	effect RamoneWoodDoormirrored -offset (0,-4*$floorlength*$floortilesize*$tile1scale,0)
	
	effect RamoneWoodDoor -offset (6,-4*$floorlength*$floortilesize*$tile1scale,0)
	effect RamoneWoodDoormirrored -offset (-6,-4*$floorlength*$floortilesize*$tile1scale,0)
end
	#tables
seti tablenum 1
setf tablesize 3
setf tablesizefactor 1.2
effect tableresized -rigid
	effect CoffeeTable -sizeScale $tablesize -rotateZ 90
end
define tablelisting(i,N)
	effect tableresized -offset (0,(0.5+&{i})*$tablesizefactor*$furnituresize*$tablesize,-0.33*$furnituresize*$tablesize)
	effect tableresized -offset (0,-(0.5+&{i})*$tablesizefactor*$furnituresize*$tablesize,-0.33*$furnituresize*$tablesize)
enddef
effect longtable
	arrayCreate tablelisting $tablenum
end

effect longtabledisplaced
	effect longtable -offset (14,-8,0)
	effect longtable -offset (-14,-8,0)
end
#pillars
effect pillar -rigid
	effect Wardrobe1
	effect Wardrobe1 -offset (0,0,4.5)
	effect Wardrobe1 -offset (0,0,9)
end

effect pillarcorrected -rigid
	effect pillar -offset (0,0.85,0)
end

effect pillarresized -rigid
	effect pillarcorrected -sizeScale 1.5
end

effect pillarsdisplaced	
	effect pillarresized -offset (4*$floorwidth*$floortilesize*$tile1scale,0.5*$floorlength*$floortilesize*$tile1scale,0)
	effect pillarresized -offset (4*$floorwidth*$floortilesize*$tile1scale,-4*$floorlength*$floortilesize*$tile1scale,0)
	
	effect pillarresized -offset (-4*$floorwidth*$floortilesize*$tile1scale,-4*$floorlength*$floortilesize*$tile1scale,0)
	effect pillarresized -offset (-4*$floorwidth*$floortilesize*$tile1scale,0.5*$floorlength*$floortilesize*$tile1scale,0)
end
#static butterflies
define staticbutterflywings(num,suffix)
	effect butterflywing&{num}static&{suffix} -hardStop -rigid
	#sequence butterflywingflap&{i}timed
		effect butterflywings&{num}offset&{suffix} -rotateY -($flaptiltangle/2)
		effect butterflywings&{num}offset&{suffix} -rotateY (180+$flaptiltangle/2)
	end
enddef

define butterflywingreindexedcoloredstatic(i,N)
	create staticbutterflywings($wingindex&{i} "colored")
enddef

arrayCreate butterflywingreindexedcoloredstatic $wingnum

define butterflywingstaticlisting(num,suffix)
	#effect butterflywingflap&{i}&{mode}
	effect butterflywing&{num}static&{suffix}
enddef

define butterflywinglistingreindexedcoloredstatic(i,N)
	create butterflywingstaticlisting($wingindex&{i} "colored")
enddef

effect staticbutterfly
	select
		arrayCreate butterflywinglistingreindexedcoloredstatic $wingnum
	end
	effect bodybutterflycolored
	effect bodybutterflycolored -rotateY 90
end

particles pin
	color (0.1,0.2,0.3)
	texture RamonePin -draw alphaTestDissolve
	size 3
	align Zpole
	life 2
	inject 1 -hold 1
	source -point
end
effect pin -rigid
	particles pin
end

effect pinnedbutterfly -rigid
	effect staticbutterfly
	effect pin
end
effect pinnedbutterflyresized -rigid
	effect pinnedbutterfly -sizeScale 0.22
end
effect pinnedbutterflydisplaced -rigid
	effect pinnedbutterflyresized -offset (0,0,0.08)
end

effect RamonePhotoFramereoriented -rigid
	effect RamonePhotoFrame -rotateX 90
end

effect butterflyframed -rigid
	effect RamonePhotoFramereoriented -rotateZ 90
	effect pinnedbutterflydisplaced -offset (0.4,0.9,0)
	effect pinnedbutterflydisplaced -offset (-0.4,0.9,0)
	effect pinnedbutterflydisplaced -offset (0.4,0.3,0)
	effect pinnedbutterflydisplaced -offset (-0.4,0.3,0)
	effect pinnedbutterflydisplaced -offset (0.4,-0.3,0)
	effect pinnedbutterflydisplaced -offset (-0.4,-0.3,0)
	effect pinnedbutterflydisplaced -offset (0.4,-0.9,0)
	effect pinnedbutterflydisplaced -offset (-0.4,-0.9,0)
end

effect butterflyframedshelf -rigid
	effect butterflyframed -rotateX -90
end

setf butterflyframeheight 3
define butterflyframedshelflisting(i,N)
	effect butterflyframedshelf -offset ((0.5+&{i})*$cabinrowgap,7,$butterflyframeheight)
	effect butterflyframedshelf -offset (-(0.5+&{i})*$cabinrowgap,7,$butterflyframeheight)
enddef
effect butterflyframedshelfdisplaced -rigid
	arrayCreate butterflyframedshelflisting $cabinrownum
end

seti numframeonwall 3
define butterflyframedwalllisting(i,N)
	effect butterflyframedshelf -offset ((0.5+&{i})*3,0,$butterflyframeheight)
	effect butterflyframedshelf -offset (-(0.5+&{i})*3,0,$butterflyframeheight)
enddef
effect butterflyframedwallrow -rigid
	arrayCreate butterflyframedwalllisting $numframeonwall
end

effect butterflyframedwallrowreoriented -rigid
	effect butterflyframedwallrow -rotateZ 90 -offset (4*$floorwidth*$floortilesize*$tile1scale-0.2,-8,1)
	effect butterflyframedwallrow -rotateZ -90 -offset (-4*$floorwidth*$floortilesize*$tile1scale+0.2,-8,1)
end
#volumetric shadow
seti shasdowsheetnum 30
setf shadowintensity 1.5
setf shadowthickness 10
particles shadow
	color (0,0,0)
	texture rectongle -draw alphaTestDissolve -sortOffset -0.1
	size 50
	align source
	life 2
	alpha $shadowintensity/$shasdowsheetnum
	#aspect 1.5
	inject 1 -hold 1
	source -point
end
effect shadow -rigid
	particles shadow
end
effect shadowreoriented -rigid
	effect shadow -rotateX 90
end

define shadowsheetlisting(i,N)
	effect shadowreoriented -offset(0,&{i}*$shadowthickness/&{N},0)
enddef

effect voumetricshadow -rigid
	arrayCreate shadowsheetlisting $shasdowsheetnum
end
effect voumetricshadowdisplaced -rigid
	effect voumetricshadow -offset (0,8,0)
end

effect voumetricshadowceiling -rigid
	effect voumetricshadow -rotateX -90
end
effect voumetricshadowceilingdisplaced -rigid
	effect voumetricshadowceiling -offset (0,0,10)
end

effect cleanbutterflyroom -rigid
	effect room1
	effect furnishing
	effect woodendoors
	effect butterflyframedshelfdisplaced
	effect butterflyframedwallrowreoriented
	effect longtabledisplaced
	effect voumetricshadowdisplaced
	effect voumetricshadowceilingdisplaced
	effect pillarsdisplaced
end

setf roomdisplacebackward 6
effect cleanbutterflyroomdisplaced -rigid
	effect cleanbutterflyroom -offset (0,$roomdisplacebackward,0)
end
#chorus room
metaparticles butterfliesburst
	yaw 0
	pitch 0 0.2
	roll 0
	alpha 0 1 1 1 1 1 1 1 1 0
	source -box (0.5,0,1)
	size 0.45*$butterflysizeinswarm -vary 0.3#0.5
	emit -dir (0,1,0) -speed 12 2
	force -worldGravity -9  
	life 0.8 #-preroll 3
	rate 3.5 -loop 1
	effect butterflytotalcolored
end
effect butterfliesburst -rigid
	metaparticles butterfliesburst
end
	#cabinet 1
seti numloop 200
effect Cabinet1wButterfly -rigid
	effect Cabinet1 -rotateZ 180
	effect butterfliesburst -offset (0,-1.5,2.8) 
end

define butterflyfloatingsetting(i)
	yaw 1#0 -vary 1
	roll 1
	pitch 1.06 #-vary 0.001
	source -point
	inject 1 
	life 2
	#emit -dir (0,0,1) 0 -speed 0.3 1
	emit -dir (0,0,1) 0 -speed -0.3 1
	
	#force -attractor (0,0,0.2) 1 1 0
	force -attractor (0,0,-0.2) 1 0 1
	
enddef

metaparticles Cabinet1wButterflyfloating
	create butterflyfloatingsetting(0)
	effect Cabinet1wButterfly
end
define Cabinet1wButterflyfloatinglisting(i,N)
	metaparticles Cabinet1wButterflyfloating
enddef
effect Cabinet1wButterflyfloating -rigid
	particlesequence
		arrayCreate Cabinet1wButterflyfloatinglisting $numloop
	end
end
	#cabinet 2
effect Cabinet2wButterfly -rigid
	effect Cabinet2 -rotateZ 180
	effect butterfliesburst -offset (0.4,-2,2.4) 
	effect butterfliesburst -offset (-0.4,-2,2.4) 
end

metaparticles Cabinet2wButterflyfloating
	create butterflyfloatingsetting(0)
	effect Cabinet2wButterfly
end
define Cabinet2wButterflyfloatinglisting(i,N)
	metaparticles Cabinet2wButterflyfloating
enddef

effect Cabinet2wButterflyfloating -rigid
	particlesequence
		arrayCreate Cabinet2wButterflyfloatinglisting $numloop
	end
end

effect butterflycabinet -rigid
	select
		effect Cabinet1wButterflyfloating -prob 0.6
		effect Cabinet2wButterflyfloating -prob 0.6
	end
end

metaparticles butterflycabinetrow
	yaw 1 -vary 0.1#5
	roll 1
	pitch 1
	source -box (1,8,2) -offset (0,0,2.5)
	life 2
	size 0.85
	inject 4 -hold 1
	effect butterflycabinet
end
effect butterflycabinetrow -rigid
	metaparticles butterflycabinetrow
end
effect butterflycabinetrowreoriented -rigid
	effect butterflycabinetrow -rotateZ 180
end

define butterflycabinetrowlisting(i,N)
	effect butterflycabinetrowreoriented -offset ((1+&{i})*$cabinrowgap,1.2*$cabindisplace,0)
	effect butterflycabinetrowreoriented -offset (-(1+&{i})*$cabinrowgap,1.2*$cabindisplace,0)
enddef

effect butterflycabinetrowexhibit -rigid
	arrayCreate butterflycabinetrowlisting -1+$cabinrownum
end

#empty frames
metaparticles empptyframerandom
	yaw 0
	pitch 0.25
	roll 1 -vary 0.1
	life 2 
	size 0.8
	inject 1 -hold 1
	source -box (0,0,0.2)
	effect RamonePhotoFramereoriented
end
effect empptyframerandom -rigid
	metaparticles empptyframerandom
end
effect emptyframecluster
	effect empptyframerandom -offset (0,-0.2,0.8)
	effect empptyframerandom -offset (0,0,0)
	effect empptyframerandom -offset (0,0.2,-0.8)
end
	#dissected rose
effect offsetutensil -rigid
	select
		effect fork 
		effect knife 
	end
end
metaparticles utensiltilted
	yaw 1 -vary 0.05
	pitch 1 -vary 0.05
	roll 1 -vary 0.05
	source -point -offset (0,0,-1)
	life 2
	size 6.5#8
	inject 3 -hold 1
	effect offsetutensil
end
effect utensiltilted -rigid
	metaparticles utensiltilted
end

Model Rosevioletsmall
	name RamoneRose_Gloss#RamoneRose
	size 0.35#0.5
	color (0.1,0,0.3)#(0.3,0.05,0)
end
effect dissectedrose -rigid
	effect utensiltilted
	select
		Model Rosevioletsmall -offset (0,0.5,-2) -rotateXYZ -30 0 0
		Model Rosevioletsmall -offset (0,0.5,-2) -rotateXYZ -30 0 180
		Model Rosevioletsmall -offset (0,0.5,-2) -rotateXYZ -30 0 90
	end
end
	#decor skull
effect decorRosevioletresized  -rigid
	effect Roseviolet -sizeScale 0.1
end
effect decorskullresized -rigid
	effect skullfloralsingle -sizeScale 0.6
end
effect decorhaloresized -rigid
	effect fullmagicglyphs4 -sizeScale 0.17
end
effect decorhaloresizedreoriented -cameraFacing
	effect decorhaloresized -rotateY 90
end

effect skullclusterdecor -hardStop
	effect decorskullresized -offset (0,0,0)
	effect decorhaloresizedreoriented -offset (0,0,1)
	effect decorRosevioletresized -offset (0,-0.7,0) -rotateXYZ -80 0 0
	effect decorRosevioletresized -offset (-0.2,-0.4,0) -rotateXYZ -90 70 0
	effect decorRosevioletresized -offset (0.2,-0.4,0) -rotateXYZ -90 -70 0
end
effect skullclusterdecorresized
	effect skullclusterdecor -sizeScale 1.4
end
effect skullclusterdecoroffset
	effect skullclusterdecorresized -offset (0,0,-0.5)
end

sequence clustersequence -loop -noOverlap
	wait			$t3a
	play emptyframecluster	$t4a-$t3a
	wait			$t5a-$t4a
	play dissectedrose	$t6a-$t5a
	wait			$t7a-$t6a
	play skullclusterdecoroffset 	$t8a-$t7a
end
effect clustersequence -rigid -hardStop
	sequence clustersequence
end

setf frameclusterradius 9.5
effect clusterdisplaced #-rigid
	#effect clustersequence -offset (0,$frameclusterradius,2.4)
	effect clustersequence -offset (0,$frameclusterradius,2.4)
end
effect clusterset
	effect clusterdisplaced -rotateZ 20
	effect clusterdisplaced -rotateZ -20
	effect clusterdisplaced -rotateZ 60
	effect clusterdisplaced -rotateZ -60
	effect clusterdisplaced -rotateZ 110
	effect clusterdisplaced -rotateZ -110
end
#darkspacepillars
effect pillarsdarkspace
	#corner pillars
	#effect pillarresized -offset (1.5*$floorlength*$floortilesize*$tile1scale,1.5*$floorlength*$floortilesize*$tile1scale,0)
	effect pillarresized -offset (1.2*$floorlength*$floortilesize*$tile1scale,-2*$floorlength*$floortilesize*$tile1scale,0)
	
	effect pillarresized -offset (-1.2*$floorlength*$floortilesize*$tile1scale,-2*$floorlength*$floortilesize*$tile1scale,0)
	#effect pillarresized -offset (-1.5*$floorlength*$floortilesize*$tile1scale,1.5*$floorlength*$floortilesize*$tile1scale,0)
	#side pillars
	effect pillarresized -offset (3*$floorlength*$floortilesize*$tile1scale,1*$floorlength*$floortilesize*$tile1scale,0)
	effect pillarresized -offset (3*$floorlength*$floortilesize*$tile1scale,-1*$floorlength*$floortilesize*$tile1scale,0)
	
	effect pillarresized -offset (-3*$floorlength*$floortilesize*$tile1scale,-1*$floorlength*$floortilesize*$tile1scale,0)
	effect pillarresized -offset (-3*$floorlength*$floortilesize*$tile1scale,1*$floorlength*$floortilesize*$tile1scale,0)
end

effect dirtychorusbutterflyroom -rigid
	effect butterflycabinetrowexhibit
	effect tile0totalfloor -rotateZ 45
	#effect butterfliescoloredwglyph
	effect pillarsdarkspace
end

setf dark 0.1#0.2
define scenesetting(i)
	yaw 0
	roll 0
	pitch 0
	inject 1
	source -point
enddef
define cleanroomdarken(index, ending, begin)
	metaparticles cleanroomdarken&{index}
		color ($dark,$dark,$dark) #($dark,$dark,$dark) ($dark,$dark,$dark) ($dark,$dark,$dark) ($dark,$dark,$dark)(1,1,1)
		create scenesetting(0)
		life ($t&{ending}-$t&{begin})
		effect cleanbutterflyroomdisplaced#cleanbutterflyroom
	end
	effect cleanroomdarken&{index} -rigid
		metaparticles cleanroomdarken&{index}
	end
enddef
define dirtyroomdarken(index, ending, begin)
	metaparticles dirtyroomdarken&{index}
		color ($dark,$dark,$dark) #($dark,$dark,$dark) ($dark,$dark,$dark) ($dark,$dark,$dark) ($dark,$dark,$dark)(1,1,1)
		create scenesetting(0)
		life ($t&{ending}-$t&{begin})
		effect dirtychorusbutterflyroom
	end
	effect dirtyroomdarken&{index} -rigid
		metaparticles dirtyroomdarken&{index}
	end
enddef

create cleanroomdarken("0" "3a" "2c")
create dirtyroomdarken("1" "4a" "3b")
create cleanroomdarken("2" "5a" "4h")
create cleanroomdarken("3" "7a" "6f")
create dirtyroomdarken("4" "10b" "10a")

sequence roomtimed -noOverlap
	wait 				($t2)
	
	play cleanbutterflyroomdisplaced		($t2c-$t2)
	play cleanroomdarken0		($t3a-$t2c)
	play dirtychorusbutterflyroom	($t3b-$t3a) #1st chorus
	play dirtyroomdarken1		($t4a-$t3b)
	
	play cleanbutterflyroomdisplaced		($t4h-$t4a)
	play cleanroomdarken2		($t5a-$t4h)
	play dirtychorusbutterflyroom	($t6a-$t5a) #2nd chorus
	
	play cleanbutterflyroomdisplaced		($t6f-$t6a)
	play cleanroomdarken3		($t7a-$t6f)
	play dirtychorusbutterflyroom	($t8a-$t7a) #3rd chorus
	
	wait				($t10a-$t8a)
	play dirtyroomdarken1		($t10b-$t10a)
	play dirtychorusbutterflyroom	($t11a-$t10b)
end
effect roomtimed -rigid#-hardStop
	sequence roomtimed
end
#room darken
#<setf dark 0.2
define scenesetting(i)
	yaw 0
	roll 0
	pitch 0
	inject 1
	source -point
	effect roomtimed
enddef
define roomdarken(index, ending, begin)
	metaparticles roomdarken&{index}
		color ($dark,$dark,$dark) #($dark,$dark,$dark) ($dark,$dark,$dark) ($dark,$dark,$dark) ($dark,$dark,$dark)(1,1,1)
		create scenesetting(0)
		life ($&{ending}-$&{begin})
	end
enddef
define roomnormal(index ,ending, begin)
	metaparticles roomnormal&{index}
		color (1,1,1)# (1,1,1)
		create scenesetting(0)
		life ($&{ending}-$&{begin})
	end
enddef

create roomnormal("0" "t2c" "t0")
create roomdarken("1" "t3a" "t2c")
create roomnormal("2" "t3b" "t3a")
create roomdarken("3" "t3a" "t2c")
create roomnormal("4" "t3b" "t3a")
create roomdarken("5" "t4a" "t3b")
create roomnormal("6" "t4h" "t4a")
create roomdarken("7" "t5a" "t4h")
create roomnormal("8" "t6f" "t5a")
create roomdarken("9" "t7a" "t6f")
create roomnormal("10" "t10a" "t7a")
create roomdarken("11" "t10b" "t10a")
create roomnormal("12" "t11a" "t10b")

effect roombrightnesscontrol -rigid -hardStop
	particlesequence
		metaparticles roomnormal0
		metaparticles roomdarken1
		metaparticles roomnormal2
		metaparticles roomdarken3
		metaparticles roomnormal4
		metaparticles roomdarken5
		metaparticles roomnormal6
		metaparticles roomdarken7
		metaparticles roomnormal8
		metaparticles roomdarken9
		metaparticles roomnormal10
		metaparticles roomdarken11
		metaparticles roomnormal12
		
	end
end
#>
#final room effect
effect Teniwoha_SAMSA_Rooms -hardStop
	effect clusterset
	effect roomtimed#roombrightnesscontrol#
end

export Teniwoha_SAMSA_Rooms